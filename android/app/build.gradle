// TODO: Rewrite as Kotlin DSL - https://developer.android.com/build/migrate-to-kotlin-dsl
import org.apache.commons.io.FileUtils
plugins {
    id 'org.jetbrains.kotlin.android'
}

def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary

if (buildAsApplication) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

// ----------------------------------------
// Read Version Info From Cargo.toml
// ----------------------------------------
// https://docs.oracle.com/javase/8/docs/api/java/io/File.html
String readCargoVersion() {
    File cargoTomlLocation = new File(buildscript.sourceFile.parentFile.parentFile.parentFile, "Cargo.toml")
    ArrayList<String> cargoTomlContents = cargoTomlLocation.readLines()
    String cargoVersionLine = cargoTomlContents.find({ line -> line.toLowerCase().startsWith("version") })

    return cargoVersionLine.split("\"", 3)[1]
}

static int getAPKVersionCode(String cargoVersion) {
    // cargoVersion = "999.888.777-alpha.6"
    String[] cargoVersionSplit = cargoVersion.split('\\.', 3)
    String[] prereleaseSplit = cargoVersionSplit[2].split('\\-', 2)

    // Version Strings
    String majorString = cargoVersionSplit[0]
    String minorString = cargoVersionSplit[1]
    String patchString = prereleaseSplit[0].split('\\+', 2)[0]

    // Debugging
    // println String.format("Test Version Parse: %s.%s.%s", majorString, minorString, patchString)

    // Semver Numbers
    int major = Integer.parseInt(majorString) * 1000000 // 1,000,000
    int minor = Integer.parseInt(minorString) * 1000    //     1,000
    int patch = Integer.parseInt(patchString)           //         1

    // TODO: Figure out how to deal with 0.40.0 and 4.0.0
    // Max Java Integer: 2,147,483,647
    // Max Version Code: 2,100,000,000
    // 999.888.777-alpha.6 -> 999,000,000 + 888,000 + 777 = 999,888,777
    // println String.format("Test Version Code: %d.%d.%d", major, minor, patch)
    return major + minor + patch
}
// ----------------------------------------

// ----------------------------------------
// Copy Assets From Engine
// ----------------------------------------
void copyAssetsFromEngine() {
    File fromAssetsLocation = new File(buildscript.sourceFile.parentFile.parentFile.parentFile, "resources/assets")
    File toAssetsLocation = new File(buildscript.sourceFile.parentFile, "src/main/assets")

    try {
        println "Copy Assets From " + fromAssetsLocation + " To " + toAssetsLocation
        FileUtils.deleteDirectory(toAssetsLocation)
        FileUtils.copyDirectory(fromAssetsLocation, toAssetsLocation)
    } catch (IOException e) {
        e.printStackTrace()
    }
}
// ----------------------------------------

String cargoVersion = readCargoVersion()
int cargoVersionCode = getAPKVersionCode(cargoVersion)
println "Building Catgirl Engine v" + cargoVersion + " (" + cargoVersionCode + ")"
copyAssetsFromEngine()

android {
    namespace 'land.catgirl.engine'
    compileSdk 34

    defaultConfig {
        if (buildAsApplication) {
            applicationId "land.catgirl.engine"
        }

        minSdk 30
        targetSdk 34
        versionName cargoVersion
        versionCode cargoVersionCode

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.env.ANDROID_RELEASE_KEY_PATH)
            storePassword System.env.ANDROID_RELEASE_STORE_PASSWORD
            keyAlias "key0"
            keyPassword System.env.ANDROID_RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false  // until fix for com.android.application 8.1.0+ minify breakage
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        prefab true
    }
    packagingOptions {
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
    }

}

apply plugin: 'com.github.willir.rust.cargo-ndk-android'

cargoNdk {
    targets = ["arm64", "arm", "x86", "x86_64"]
    module = ".."
    extraCargoBuildArguments = ["--lib"]

    buildTypes {
        release {
            buildType = "release"
        }
        debug {
            buildType = "debug"
        }
    }
}

dependencies {
    // https://maven.google.com/web/index.html?q=androidx.games#androidx.games:games-activity
    implementation 'androidx.games:games-activity:2.0.2'  // Do Not Update RN

    // https://mvnrepository.com
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'commons-io:commons-io:2.15.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
